ROUTINE AoC2020Day2

/// key takeaways: be good at parsing data
Start() PUBLIC {
	d readInput("/irisdev/app/inputs/2020/input2.txt", .nums)
	
	return $$p1(.nums)_"^"_$$p2(.nums)
}

p1(x) PUBLIC {
	s t = 0
	for i=1:1:x {
		s l=x(i).Group(1), h=x(i).Group(2), c=x(i).Group(3), w=x(i).Group(4)
		s len = $length(w, c)-1
		if (l <= len) && (len <= h) {
			s t = t+1
		}
	}
	return t
}

p2(x) PUBLIC {
		s t = 0
	for i=1:1:x {
		s l=x(i).Group(1), h=x(i).Group(2), c=x(i).Group(3), w=x(i).Group(4)
		s len = $length(w, c)-1
		if (($e(w, l)=c)+($e(w, h)=c) = 1) {
			s t = t+1
		}
	}
	return t
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s line = $translate(file.ReadLine(), $c(13))
		s result(i) = ##class(%Regex.Matcher).%New("([0-9]+)-([0-9]+) (.): (.+)", line)
		d result(i).Match()
	}
	s result = i
}