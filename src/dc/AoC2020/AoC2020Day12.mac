ROUTINE AoC2020Day12

/// main takeaways: reuse code from AoC2012Day6... :)

Start() PUBLIC {
	s input="/irisdev/app/inputs/2020/input12.txt"
	d readInput(input, .in)
	
	s ans1 = $$getDist(.in)
	s ans2 = $$getDist2(.in)
	
	return ans1_"^"_ans2
}

getDist(in) PUBLIC {
	s cx=0, cy=0
	s cdirx=1, cdiry = 0
	
	
	for i=1:1:in {
		s d = $p(in(i), "^"), n = $p(in(i), "^", 2)
		
		if d="N" {
			s cy = cy + n
		} elseif d="S" {
			s cy = cy - n
		} elseif d="E" {
			s cx = cx + n
		} elseif d="W" {
			s cx = cx - n
		} elseif d="L" {
			for j=1:1:n/90 {
				s dummy = cdirx
				s cdirx = -cdiry
				s cdiry = dummy
			}
		} elseif d="R" {
			for j=1:1:n/90 {
				s dummy = cdirx
				s cdirx = cdiry
				s cdiry = -dummy
			}
			
		} elseif d="F" {
			s cx = cx + (cdirx * n)
			s cy = cy + (cdiry * n)
		}
	}
	return $zabs(cx)+$zabs(cy)
}

getDist2(in) PUBLIC {
	s cx=0, cy=0
	s cdirx=1, cdiry = 0
	s wx=10, wy=1
	
	for i=1:1:in {
		s d = $p(in(i), "^"), n = $p(in(i), "^", 2)
		
		if d="N" {
			s wy = wy + n
		} elseif d="S" {
			s wy = wy - n
		} elseif d="E" {
			s wx = wx + n
		} elseif d="W" {
			s wx = wx - n
		} elseif d="L" {
			for j=1:1:n/90 {
				s dummy = wx
				s wx = -wy
				s wy = dummy
			}
		} elseif d="R" {
			for j=1:1:n/90 {
				s dummy = wx
				s wx = wy
				s wy = -dummy
			}
			
		} elseif d="F" {
			s cx = cx + (wx * n)
			s cy = cy + (wy * n)
		}
	}
	return $zabs(cx)+$zabs(cy)
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s curr = $translate(file.ReadLine(), $c(13))
		s result(i) = $e(curr, 1)_"^"_$e(curr, 2, *)
	}
	s result = i
}

