ROUTINE AoC2020Day8

Start() PUBLIC {
	s input="/irisdev/app/inputs/2020/input8.txt"
	d readInput(input, .code)

	s ans1 = $$run(.code, 1)
	s ans2 = 0
	for i=1:1:code{
		if $extract(code(i), 1, 3)="nop" {
			s code(i) = $replace(code(i), "nop", "jmp")
			s ans2 = $$run(.code)
			if ans2 '= 0 quit
			s code(i) = $replace(code(i), "jmp", "nop")
		} elseif  $extract(code(i), 1, 3)="jmp" {
			s code(i) = $replace(code(i), "jmp", "nop")
			s ans2 = $$run(.code)
			if ans2 '= 0 quit
			s code(i) = $replace(code(i), "nop", "jmp")
		}
	}
	
	return ans1_"^"_ans2
}

run(code, p1=0) PUBLIC {
	s ind = 1
	s acc = 0
	
	for {
		if (ind = (code+1)) return acc
		if ((ind <= 0) || (ind > code)) {
			return $s(p1: acc, 1:0)
		}
		if $d(seen(ind)) {
			return $s(p1: acc, 1:0)
		}
		s seen(ind) = ""
		
		s ins=$p(code(ind), " "), val=$p(code(ind), " ", 2)
		if (ins = "nop") {
			s ind = ind +1
		} elseif (ins = "acc") {
			s acc = acc + val
			s ind = ind + 1
		} else {
			s ind = ind + val
		}
	}
	return 0
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s result(i) = $translate(file.ReadLine(), $c(13))
		
	}
	s result = i
}

