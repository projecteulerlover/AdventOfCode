ROUTINE AoC2017Day22

Start() PUBLIC {
	s input="/irisdev/app/inputs/2017input22.txt"
	d readInput(input, .in)

	s ans1 = $$p1(.in)

    kill in
    d readInput(input, .in)

    s ans2 = $$p2(.in)
	return ans1_"^"_ans2
}

p1(in, t=10000) PUBLIC {
    s cx=(in+1)/2, cy=(in+1)/2
    s dx=-1, dy=0
    s total=0
    for i=1:1:t {
        s infected=$g(in(cx_","_cy), 0)
        if infected=1 {
            s tmp = dx
            s dx=dy, dy=-tmp
            k in(cx_","_cy)
        } else {
            s tmp = dx
            s dx=-dy, dy=tmp
            s in(cx_","_cy)=1
            s total =total +1
        }
        s cx=cx+dx, cy=cy+dy

    }

    return total
}

p2(in, t=10000000) PUBLIC {
    s cx=(in+1)/2, cy=(in+1)/2
    s dx=-1, dy=0
    s total=0
    for i=1:1:t {
        s state=$g(in(cx_","_cy), 0)
        if state=1 {
            s tmp = dx
            s dx=dy, dy=-tmp
            s in(cx_","_cy)=3
        } elseif state=0 {
            s tmp = dx
            s dx=-dy, dy=tmp
            s in(cx_","_cy)=2
        } elseif state=2 {
            s total = total+1
            s in(cx_","_cy)=1
        } else { // state=3
            s dx=-dx, dy=-dy
            k in(cx_","_cy)
        }
        s cx=cx+dx, cy=cy+dy

    }

    return total
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
        s i = i+1
        s curr = $translate(file.ReadLine(), $c(13))
        for j=1:1:$length(curr) {
            if $e(curr, j)="#" s result(i_","_j) = 1
        }
	}
	s result = i
}

