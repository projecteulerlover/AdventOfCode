ROUTINE AoC2021Day10

Start() public {
	d readInput("/irisdev/app/inputs/2021/10.in", .input)

    return $$p1(.input)_"^"_$$p2(.input)
}

p1helper(l, m, o) public {
	s stack = []
	for j=1:1:$l(l) {
		s c = $e(l, j)
		if $d(m(c)) {
			d stack.%Push(c)
			continue
		}
		s x = stack.%Pop()
		if o(c)'=x {
			return m(o(c))
		}
	}
	return 0
}

p1(x) public {
	s m("(")=3, m("[")=57, m("{")=1197, m("<")=25137
	s o(")")="(", o("]")="[", o("}")="{", o(">")="<"
    s t=0
	for i=1:1:x {
		s t = t + $$p1helper(x(i), .m, .o)
	}
	return t
}

p2helper(l, m, o) public {
	s stack = []
	for j=1:1:$l(l) {
		s c = $e(l, j)
		if $d(m(c)) {
			d stack.%Push(c)
			continue
		}
		s x = stack.%Pop()
		if o(c)'=x {
			return []
		}
	}
	return stack
}

p2(x) public {
	s m("(")=1, m("[")=2, m("{")=3, m("<")=4
	s o(")")="(", o("]")="[", o("}")="{", o(">")="<"
    s count=0
	for i=1:1:x {
		s stack = $$p2helper(x(i), .m, .o)
		s ct = 0
		for j=stack.%Size()-1:-1:0 {
			s c = stack.%Get(j)
			s ct = ct*5 + m(c)
		}
		#; luckily no duplicates for my input...
		if ct s t(ct)="", count=count+1
	}
	zw t
	s d=""
	for i=0:1:count\2 {
		s d = $o(t(d))
	}
	return d
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s line = $translate(file.ReadLine(), $c(13))
		s result(i) = line
	}
	s result = i
}