ROUTINE AoC2021Day18

Start() public {
	d readInput("/irisdev/app/inputs/2021/18.in", .x)
	return $$p1(.x)_"^"_$$p2(.x)
}

reduce(cc) public {
	s exploded = 0
	s vs = $$explode(cc, 4, .exploded)
	s cc = $lg(vs, 3)
	if exploded {
		d reduce(.cc)
	}
	s splat = 0
	s cc = $$split(cc, .splat)
	if splat {
		d reduce(.cc)
	}
}

split(cc, splat) public {
	if $lv(cc) {
		s l = $lg(cc, 1), r = $lg(cc, 2)
		s nl = $$split(l, .splat)
		if splat return $lb(nl, r)
		s nr = $$split(r, .splat)
		if splat return $lb(l, nr)
	}
	if cc >= 10 {
		s splat = 1
		return $lb(cc\2, (cc+1)\2)
	}
	return cc
}

add(cc, n, s) {
	if n="" return cc
	if ($lv(cc)) {
		if s=0 return $lb($$add($lg(cc, 1), n, s), $lg(cc,2))
		return $lb($lg(cc,1), $$add($lg(cc, 2), n, s))
	}
	return cc+n
}

explode(cc, d, exploded) public {
	if $lv(cc) {
		if d = 0 {
			s exploded = 1
			return $lb($listget(cc, 1), $listget(cc, 2), 0)
		}
		s l = $lg(cc, 1), r = $lg(cc, 2)
		s vs = $$explode(l, d-1, .exploded)
		if exploded {
			s nl = $lg(vs, 1), nr = $lg(vs, 2), l = $lg(vs, 3)
			return $lb(nl, "", $lb(l, $$add(r, nr, 0)))
		}
		s vs = $$explode(r, d-1, .exploded)
		if exploded {
			s nl = $lg(vs, 1), nr = $lg(vs, 2), r = $lg(vs, 3)
			return $lb("", nr, $lb($$add(l, nl, 1), r))
		}
	}
	return $lb("", "", cc)
}

getM(cc) public {
	if $lv(cc) {
		return (3*$$getM($lg(cc, 1)))+(2*$$getM($lg(cc, 2)))
	}
	return cc
}

p1(x) public {
	s c = x(1)
	for i=2:1:x {
		s r = x(i)
		s c = $lb(c, r)
		d reduce(.c)
	}
	return $$getM(c)
}

p2(x) public {
	s M = 0
	for i=1:1:x {
		s l1 = x(i)
		for j=1:1:x {
			if i=j continue
			s l2 = x(j)
			s c = $lb(l1, l2)
			d reduce(.c)
			s m = $$getM(c)
			if m > M s M=m
		}
	}
	return M
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s l = $translate(file.ReadLine(), $c(13))
		s l = $replace(l, "[", "$lb(")
		s l = $translate(l, "]", ")")
		s cmd = "(x) s x="_l_" QUIT 0"
		s v = $XECUTE(cmd, .x)
		s result(i) = x
	}
	s result = i
}