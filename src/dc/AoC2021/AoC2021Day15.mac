ROUTINE AoC2021Day15

Start() public {
	d readInput("/irisdev/app/inputs/2021/15.in", .input)

    return $$p1(.input)_"^"_$$p2(.input)
}

p1(x) public {
	s MAX = 9*x*x

	s dr(1)="0 1", dr(2)="1 0", dr(3)="-1 0", dr(4)="0 -1"

	for i=1:1:x {
		for j=1:1:x {
			s ds(i, j) = MAX
		}
	}
	s q(0, "1 1") = ""

	while $d(q) {
		s d = $o(q(""))
		s ij = $o(q(d, ""))
		s i = $p(ij, " ", 1), j = $p(ij, " ", 2)
		k q(d, ij)

		s cr = d + x(i, j)
		if cr < ds(i, j) {
			s ds(i, j) = cr

			for k=1:1:4 {
				s ni = i + $p(dr(k), " ", 1)
				s nj = j + $p(dr(k), " ", 2)

				if (1<=ni) && (ni<=x) && (1<=nj) && (nj<=x) {
					s q(cr, ni_" "_nj) = ""
				}
			}
		}
	}
	return ds(x, x) - ds(1, 1)
}

p2(x) public {
	m y = x
	s y = 5*x
	for i=1:1:y {
		for j=1:1:y {
			s di=i\x - $s(i#x: 0, 1: 1), ri=$s(i#x: i#x, 1:x)
			s dj=j\x - $s(j#x: 0, 1: 1), rj=$s(j#x: j#x, 1:x)

			s v = x(ri, rj) + di + dj
			if v>9 s v=v-9
			s y(i, j)=v
		}
	}
	return $$p1(.y)
}

readInput(input, result) PUBLIC {
	set file = ##class(%Stream.FileCharacter).%New()
	s sc = file.LinkToFile(input)
	s i = 0
	while 'file.AtEnd {
		s i = i+1
		s line = $translate(file.ReadLine(), $c(13))
		for j=1:1:$l(line) {
			s result(i, j) = $e(line, j)
		}
	}
	s result = i
}